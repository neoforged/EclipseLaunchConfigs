plugins {
    id 'java-library'
    id 'net.neoforged.gradleutils'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

group = 'net.neoforged'
version = gradleutils.getTagOffsetVersion()

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:0.4.2'
    compileOnly 'com.github.bsideup.jabel:jabel-javac-plugin:0.4.2'
    compileOnly 'org.jetbrains:annotations:24.0.1'
}

// Uses https://github.com/bsideup/jabel to compile to Java 8 from Java 16 source.
configure([tasks.compileJava]) {
    sourceCompatibility = 17 // for the IDE support
    options.release = 8

    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

sourceSets.test.java {
    srcDir 'src/test/java'
}

tasks.register('hello') {
    doLast {
        println 'Hello from Gradle!'
        if (project.hasProperty('helloReply')){
        	println helloReply
        }
        // Test for --no-daemon launches to verify that the task was invoked.
    	new File(projectDir, "hello.txt").text = "Hello from Gradle inside a file!"
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username.set(System.getenv('SONATYPE_USER') ?: '')
            password.set(System.getenv('SONATYPE_PASSWORD') ?: '')
            nexusUrl.set(uri('https://s01.oss.sonatype.org/service/local/'))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = 'A Java library for generating Eclipse Launch Configurations (.launch files).'
                url = 'https://github.com/NeoForged/EclipseLaunchConfigs'

                scm {
                    url = 'https://github.com/NeoForged/EclipseLaunchConfigs'
                    connection = 'scm:git:git://github.com/NeoForged/EclipseLaunchConfigs.git'
                    developerConnection = 'scm:git:git@github.com:NeoForged/EclipseLaunchConfigs.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/NeoForged/EclipseLaunchConfigs/issues'
                }

                licenses {
                    license {
                        name = 'LGPL 2.1'
                        url = 'https://github.com/NeoForged/EclipseLaunchConfigs/blob/main/LICENSE'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'neoforged'
                        name = 'NeoForged'
                        email = 'contact@neoforged.net'
                        url = 'https://github.com/NeoForged/'
                    }
                }
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}

changelog {
    fromTag '0.1'
}

if (System.getenv('GPG_PRIVATE_KEY')) {
    signing {
        final signingKey = System.getenv('GPG_PRIVATE_KEY') ?: ''
        final signingPassword = System.getenv('GPG_KEY_PASSWORD') ?: ''
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}