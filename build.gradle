import net.neoforged.gradleutils.PomUtilsExtension.License

plugins {
    id 'java-library'
    id 'net.neoforged.gradleutils' version '3.+'
    id 'maven-publish'
}

group = 'net.neoforged'
version = gradleutils.version

repositories {
    mavenCentral()
}

gradleutils {
    setupSigning()
    setupCentralPublishing()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:1.0.0'
    annotationProcessor("net.java.dev.jna:jna-platform:5.13.0")
    compileOnly 'com.github.bsideup.jabel:jabel-javac-plugin:1.0.0'
    compileOnly 'org.jetbrains:annotations:24.0.1'

    testImplementation(libs.junit.api)
    testRuntimeOnly(libs.junit.engine)
}

// Uses https://github.com/bsideup/jabel to compile to Java 8 from Java 16 source.
configure([tasks.compileJava]) {
    sourceCompatibility = 17 // for the IDE support
    options.release = 8

    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

sourceSets.test.java {
    srcDir 'src/test/java'
}

tasks.withType(Javadoc).configureEach {
    options.tags = [
            'apiNote:a:<em>API Note:</em>',
            'implSpec:a:<em>Implementation Requirements:</em>',
            'implNote:a:<em>Implementation Note:</em>'
    ]
    options.addStringOption('Xdoclint:all,-missing', '-public')
}

test {
    javaLauncher.set(javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    })
    useJUnitPlatform()
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
            gradleutils.sign(it)

            pom {
                name = project.name
                description = 'A Java library for generating Eclipse Launch Configurations (.launch files).'

                pomUtils.neoForgedDeveloper(it)
                pomUtils.license(it, License.LGPL_v2)
                pomUtils.githubRepo(it, 'EclipseLaunchConfigs')
            }
        }
    }
    repositories {
        maven gradleutils.publishingMaven
    }
}

changelog {
    from '0.1'
}